<?xml version="1.0" encoding="UTF-8"?>
<!--
// author    : jzamora
// copyright : Rameses Systems Inc
-->
<workunit>
    <invokers>
        <invoker type="miscrysetting.create" action="create" caption="Create Miscellaneous RY Settting" target="window"/>
        <invoker type="miscrysetting.open" action="open" caption="Edit Miscellaneous RY Settting" target="window"/>
        
        <invoker type="formActions" action="_close" caption="Close" />
        <invoker type="formActions" action="save" shortcut="ctrl S" caption="Save" mnemonic="s" icon="images/save24.png"/>
        <invoker type="formActions" action="remove" shortcut="ctrl D" immediate="true" caption="Delete" mnemonic="d" icon="images/delete24.png" />
    </invokers>
    <code lang="groovy">

<![CDATA[

import com.rameses.rcp.annotations.*
import com.rameses.rcp.common.*
import com.rameses.osiris2.client.*
import java.rmi.server.UID

public class MiscRYSettingController 
{
    @Binding 
    def binding
    
    @Service('MiscRYSettingService')
    def svc
    
    def entity
    def copyYear
    def settingType
    def mode
    def errorMsg
   
    
    /*---------------------------------------------------------------------
    * FormAction Support
    ---------------------------------------------------------------------*/
    def create() {
        mode = 'create' 
        entity = createEntity()
        if( svc.settingExists() ) {
            settingType = 'copySetting'
            return 'copy'
        }
        else {
            settingType = 'newSetting'
            return 'new' 
        }
    }
    
    void open() {
        mode = 'view' 
        entity = svc.open( entity.objid )
        assessLevels = entity.assesslevels
        miscItemValues = entity.miscitemvalues
        entity.remove( 'assesslevels' )
        entity.remove( 'miscitemvalues' )
    }
    
    def createSetting() {
        if( settingType == 'newSetting' ) {
            entity = svc.create( entity )
        }
        else {
            entity = svc.copySetting( entity.ry, copyYear );
            initVariables();
        }
        return 'default'
    }
    
    void initVariables() {
        assessLevels = entity.assessLevels;
        selectedAssessLevel = ( assessLevels ? assessLevels[0] : null )
        miscItemValues = entity.miscitemvalues;
        
        entity.remove('assesslevels');
        entity.remove('miscitemvalues');
    }
    
    void save() {
        entity = svc.update( entity )
    }
    
    def remove() {
        if( MsgBox.confirm('Delete setting?') ) {
            svc.remove( entity.objid )
            return '_close'
        }
        return 'default'
    }
    
    

    /*---------------------------------------------------------------------
    *
    * AssessLevel Support
    *
    ---------------------------------------------------------------------*/
    def selectedAssessLevel
    def selectedRangeLevel
    def assessLevels = [] 
    
    def assessLevelListHandler = [
        createItem : { return createAssessLevel() },
        getRows    : { return 50 },
        getColumns : { return [
            new Column(name:'code', caption:'Code*', maxWidth:60, editable:true ),
            new Column(name:'name', caption:'Name*', editable:true ),
            new Column(name:'fixrate', caption:'Fix?*', type:'boolean', maxWidth:40, editable:true ),
            new Column(name:'rate', caption:'Rate*', type:'decimal', editable:true ),
        ]},
        validate       : { li -> validateAssessLevel( li.item ) },
        onRemoveItem   : { item -> removeAssessLevel( item ) },
        onAddItem      : { item -> assessLevels.add( item ) },
        onColumnUpdate : { item, colName -> 
            if( colName == 'fixrate' && item.fixrate == true ) {
                selectedAssessLevel?.ranges?.clear()
                rangeLevelListHandler.load()
            }
        },
        fetchList      : { return assessLevels },
    ] as SubListModel
    
    
    def rangeLevelListHandler = [
        createItem : { return createRangeAssessLevel() },
        getRows    : { return 50 },
        getColumns : { return [
            new Column(name:'mvfrom', caption:'Market Value From', type:'decimal'),
            new Column(name:'mvto', caption:'Market Value To', type:'decimal', editable:true, editableWhen:'#{root.allowRangeColumnEdit[item]}' ),
            new Column(name:'rate', caption:'Rate*', type:'decimal', editable:true, editableWhen:'#{root.allowRangeColumnEdit[item]}' ),
        ]},
        validate     : { li -> validateAssessLevelRange( li.item ) },
        onAddItem    : { item -> 
            selectedAssessLevel.ranges.add( item ) 
            svc.saveAssessLevel( selectedAssessLevel )
        },
        onRemoveItem   : { item -> removeRangeItem( item ) },
        fetchList    : { return selectedAssessLevel.ranges  },
    ] as SubListModel
    
    def allowRangeColumnEdit = { item ->
        if( ! selectedAssessLevel ) return false
        if( selectedAssessLevel.fixrate == true ) return false
        if( isFirstItem( selectedAssessLevel.ranges ) ) return true
        if( isNewEntry( item.mvfrom ) ) return true
        return isLastItem( selectedAssessLevel.ranges, item )
    } as Map
    
    void validateAssessLevel( al ) {
        try {
            required(al.code, 'Code')
            required(al.name, 'Name')
            validateRate( al.rate )
            checkDuplicateAssessLevel( al )
            svc.saveAssessLevel( al ) 
            clearError()
        }
        catch( e ) {
            showError( e )
        }
    }
    
    void checkDuplicateAssessLevel( al ) {
        def item = assessLevels.find{ it.objid != al.objid && it.code == al.code  }
        if( item ) throw new Exception('Duplicate code is not allowed.')    
        item = assessLevels.find{ it.objid != al.objid && it.name == al.name  }
        if( item ) throw new Exception('Duplicate name is not allowed.')    
    }
    
    void removeAssessLevel( item ) {
        if( MsgBox.confirm('Remove item?' ) ) {
            svc.removeAssessLevel( selectedAssessLevel )
            assessLevels.remove( item )
        }
    }
    
    void removeRangeItem( item ) {
        if( isLastItem( selectedAssessLevel.ranges, item ) ) {
            selectedAssessLevel.ranges.remove( item )
            svc.saveAssessLevel( selectedAssessLevel )
        }
    }
    
    void validateAssessLevelRange( range ) {
        def lastRange = null 
        if( selectedAssessLevel.ranges ) {
            lastRange = selectedAssessLevel.ranges.get( selectedAssessLevel.ranges.size() - 1 )
        }
        if( ! range.mvfrom ) {
            range.mvfrom = (lastRange ? lastRange.mvto : 0.0)
        }
        
        try {
            if( range.mvto < range.mvfrom && range.mvto != 0  ) throw new Exception('MV To must be greater than MV From')
            validateRate( range.rate )
            svc.saveAssessLevel( selectedAssessLevel )
            clearError()
        }
        catch( e ) {
            showError( e )
        }
    }
    
    
    /*---------------------------------------------------------------------
    *
    * MiscItemValue Support
    *
    ---------------------------------------------------------------------*/
    def miscItemValues = []
    
    void loadItems() {
        List items = svc.getMiscItems( entity.objid )
        List diff = determineDifference( items )
        if( diff ) {
            svc.saveMiscItemValues( diff )
            miscItemValues.addAll( diff )
            miscItemValueListHandler.load()
        }
    } 
    
    List determineDifference( items ) {
        miscItemValues.each { miv -> 
            items.remove( items.find{ it.miscitemid == miv.miscitemid }  ) 
        }
        return items
    }
    
    def miscItemValueListHandler = [
        getRows    : { return 50 },
        getColumns : { return [
            new Column(name:'miscitemcode', caption:'Code', maxWidth:80),
            new Column(name:'miscitemname', caption:'Name'),
            new Column(name:'expr', caption:'Computation Expression', editable:true  ),
        ]},
        validate     : { li -> validateMiscItemValue( li.item ) },
        fetchList    : { return miscItemValues },
    ] as SubListModel
    
    void validateMiscItemValue( miv ) {
        try {
            required( miv.expr, 'Computation Expression')
            svc.saveMiscItemValue( miv )
        }
        catch( e ) {
            showError( e )
        }
    }
    
    
    
    /*---------------------------------------------------------------------
    *
    * Create Records Support
    *
    ---------------------------------------------------------------------*/
    Map createEntity() {
        return [
            objid       : 'MRY' + new UID(),
            ry          : null,
        ]
    }
    
    Map createAssessLevel() {
        return [ 
            objid           : 'AL' + new UID(),
            miscrysettingid : entity.objid,
            fixrate         : true,
            rate            : 0.0,
            ranges          : [],
        ]
    }
    
    Map createRangeAssessLevel() {
        return [ 
            mvfrom  : null,
            mvto    : null,
            rate    : 0.0,
        ]
    }
    
    
    /*---------------------------------------------------------------------
    *
    * Common Methods 
    *
    ---------------------------------------------------------------------*/
    void required( value, caption ) {
        if( ! value ) throw new Exception(caption + ' is required.') 
    }
    
    void validateRate( rate ) {
        if( rate < 0.0 ) throw new Exception('Rate must be greater than or equal to zero.')
        if( rate > 100) throw new Exception('Rate must not exceed 100.00')
    }
    
    def isFirstItem( list ) {
        return list.size() == 0
    }
    
    def isNewEntry( value ) {
        return value == null 
    }
    
    def isLastItem( list, item ) {
        def index = list.indexOf( item )
        if( index < 0 ) 
            index = list.size()
        else
            index += 1 
        return list.size() == index 
    }
    
    void showError( e ) {
        println e.message
        errorMsg = e.message
        binding.refresh('errorMsg')
        throw e
    }   
    
    void clearError() {
        errorMsg = null
        binding.refresh('errorMsg') 
    }
    
}


]]>

    </code>
    
    <pages>
        <page template="etracs2.rpt.ry.misc.MiscRYSettingPage"/>
        <page name="new" template="etracs2.rpt.ry.RYSettingNewPage"/>
        <page name="copy" template="etracs2.rpt.ry.RYSettingCopyPage"/>
    </pages>
</workunit>
