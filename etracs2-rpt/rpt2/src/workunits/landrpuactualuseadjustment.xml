<?xml version="1.0" encoding="UTF-8"?>
<workunit>
    <invokers>
        <invoker type="landrpuactualuseadjustment.open" action="init" caption="Open" target="popup" />
        
        <invoker type="formActions" action="ok" caption="OK" mnemonic="o" default="true" />
        <invoker type="formActions" action="_close" caption="Cancel" mnemonic="c" immediate="true"  />
        
    </invokers>
    <code lang="groovy">
        
<![CDATA[
import com.rameses.rcp.annotations.* 
import com.rameses.rcp.common.* 
import com.rameses.osiris2.client.* 
import java.rmi.server.UID

public class LandRPUActualUseAdjustmentController 
{
    @Binding
    def binding
    
    @Service('LandRPUService')
    def svc

    //handler 
    def onupdate
    
    def rpu
    def landDetail
    def selectedItem 
    def errorMsg 
    
    void init() {
    }
    
    def ok() {
        if( onupdate ) onupdate( rpu )
        return '_close'
    }
    
    def getLookupAdjustment() {
        return InvokerUtil.lookupOpener('landadjustmenttype.lookup', [classid:rpu.classid, ry:rpu.ry] )
    }
    
    def listHandler = [
        createItem : { return createAdjustment() },
        getColumns : { return [
            new Column(name:'adj', caption:'Code*', type:'lookup', handler:lookupAdjustment,  expression:'#{adjtypecode}', minWidth:80, editable:true ),
            new Column(name:'adjtypename', caption:'Name'),
            new Column(name:'expr', caption:'Computation'),
            new Column(name:'adjustment', caption:'Adjustment', type:'decimal', minWidth:150 ),
        ]},
        validate       : { li -> validateAdjustment( li.item ) },
        onRemoveItem   : { item -> removeAdjustment( item ) },
        onAddItem      : { item -> landDetail.adjustments.add( item )  },
        onColumnUpdate : { item, colName -> 
            if( colName == 'adj' ) updateAdjustmentInfo( item )
        },
        fetchList      : { return landDetail.adjustments },
    ] as SubListModel
    
    void validateAdjustment( item ) {
        try {
            required( 'Code', item.adjtypeid )
            checkDuplicate( item )
            calculateAdjustment( item )
            clearError()
        } 
        catch(  e ) {
            showError( e )
        }
    }
    
    void removeAdjustment( item ) {
        if( MsgBox.confirm( 'Delete selected item?' ) ) {
            def ld = svc.removeActualUseAdjustment( landDetail, item )
            landDetail.clear()
            landDetail.putAll( ld )
        }
    }
    
    void updateAdjustmentInfo( item ) {
        item.adjtypeid =  item.adj?.adjtypeid 
        item.adjtypecode =  item.adj?.adjtypecode 
        item.adjtypename =  item.adj?.adjtypename 
        item.expr =  item.adj?.expr 
        item.params.addAll(item.adj?.params)
        item.adj?.remove('params') 
    }
    
    void calculateAdjustment( item ) {
        def adj = svc.calculateLandActualUseAdjustment( landDetail, item)
        item.clear()
        item.putAll(adj)
    }
    
    void checkDuplicate( item ) {
        def data = landDetail.adjustments.find{ it.adjtypeid == item.adjtypeid }
        if( data ) throw new Exception('Duplicate Adjustment is not allowed.')    
    }
    
    /*---------------------------------------------------------------
    *
    * MIscellaneous Support
    *
    ---------------------------------------------------------------*/
    Map createAdjustment() {
        return [
            objid           : 'A' + new UID(),
            adjustment      : 0.0,
            params          : [],
        ]
    }
    
    void required( caption, value ) {
        if( !value ) throw new Exception( caption + ' is required.')
    }
    
    void showError( e ) {
        println e.message
        errorMsg = e.message
        binding.refresh('errorMsg')
        throw e
    }   
    
    void clearError() {
        errorMsg = null
        binding.refresh('errorMsg') 
    }
}


]]>
    </code>
    
    <pages>
        <page template="etracs2.rpt.faas.rpu.land.LandAdjustmentPage"/>
    </pages>
</workunit>
