<?xml version="1.0" encoding="UTF-8"?>
<workunit>
    <invokers>
        <invoker type="landrysetting.create" action="create" caption="Create Land" mnemonic="b" icon="images/doc-new.png" target="window"/>
        <invoker type="landrysetting.open" action="open" caption="Edit Land" mnemonic="b" icon="images/edit.png" target="window"/>
        
        <invoker type="formActions" action="_close" caption="Close" icon="images/cancel.png"/>
        <invoker type="formActions" action="save" shortcut="ctrl S" caption="Save" mnemonic="s" icon="images/save.png"/>
        <invoker type="formActions" action="remove" shortcut="ctrl D" immediate="true" caption="Delete" mnemonic="d" icon="images/doc-delete.png" />
        
    </invokers>
    <code lang="groovy">

<![CDATA[

import com.rameses.rcp.annotations.*
import com.rameses.rcp.common.*
import com.rameses.osiris2.client.*
import java.rmi.server.UID

public class LandRYSettingController 
{
    @Binding 
    def binding
    
    @Service('LandRYSettingService')
    def svc
    
    def entity;
    def copyYear;
    def settingType;
    def mode;
    
    def landadjustments = [];
    def landactualuseadjustments = [];

    
    def create() {
        entity = createEntity();
        if( svc.settingExists() ) {
            settingType = 'copySetting';
            return 'copy';
        }
        else {
            settingType = 'newSetting';
            return 'new';
        }
    }
    
    void open() {
        def data = svc.open( entity.objid );
        entity = data.entity;
        assessLevels = data.assessLevels;
        lcuv = data.lcuv;
        landadjustments = data.landadjustments;
        landactualuseadjustments = data.landactualuseadjustments;
        entity.remove('lcuv');
        entity.remove('landadjustments');
        entity.remove('landactualuseadjustments');
    }
    
    def createSetting() {
        if( settingType == 'newSetting' ) {
            entity = svc.create( entity );
            //lcuv = entity.lcuv;
            //entity.remove( "lcuv" );
        }
        else {
            entity = svc.copySetting( entity.ry, copyYear );
        }
        initVariables();
        return 'default'
    }
    
    void initVariables() {
        assessLevels = entity.assesslevels;
        selectedAssessLevel = ( assessLevels ? assessLevels[0] : null )
        lcuv = entity.lcuv;
        selectedLCUV = ( lcuv ? lcuv[0] : null );
        selectedSpecificClass = ( selectedSpecificClass ? selectedSpecificClass[0] : null );
        selectedSubClass = ( selectedSubClass ? selectedSubClass[0] : null );
        selectedStripping = ( selectedStripping ? selectedStripping[0] : null );
        landadjustments = entity.landadjustments;
        landactualuseadjustments = entity.landactualuseadjustments;
        
        entity.remove('assesslevels');
        entity.remove('lcuv');
        entity.remove('landadjustments');
        entity.remove('landactualuseadjustments');
    }
    
    void save() {
        entity = svc.update( entity );
    }
    
    def remove() {
        if( MsgBox.confirm('Delete setting?') ) {
            svc.remove( entity.objid )
            return '_close'
        }
        return 'default'
    }

    /*---------------------------------------------------------------------
    *
    * AssessLevel Support
    *
    ---------------------------------------------------------------------*/
    def selectedAssessLevel
    def selectedRangeLevel
    def assessLevelMsg 
    def assessLevels = [] 
    
    def assessLevelListHandler = [
        createItem : { return createAssessLevel() },
        getRows    : { return 50 },
        getColumns : { return [
            new Column(name:'classcode', caption:'Code*', maxWidth:60, editable:true ),
            new Column(name:'classname', caption:'Name*', editable:true ),
            new Column(name:'fixrate', caption:'Fix?*', type:'boolean', maxWidth:40, editable:true ),
            new Column(name:'rate', caption:'Rate (%)*', type:'decimal', editable:true ),
        ]},
        validate       : { li -> doValidateAssessLevel( li.item ) },
        onRemoveItem   : { item -> 
            doRemoveItem( assessLevels, item) 
            svc.removeAssessLevel( selectedAssessLevel )
        },
        onAddItem      : { item -> assessLevels.add( item ) },
        onColumnUpdate : { item, colName -> 
            if( colName == 'fixrate' && item.fixrate == true ) {
                selectedAssessLevel?.ranges?.clear()
                rangeLevelListHandler.load()
            }
        },
        fetchList      : { return assessLevels },
    ] as SubListModel
    
    
    def rangeLevelListHandler = [
        createItem : { return createRangeAssessLevel() },
        getRows    : { return 50 },
        getColumns : { return [
            new Column(name:'mvfrom', caption:'Market Value From', type:'decimal'),
            new Column(name:'mvto', caption:'Market Value To', type:'decimal', editable:true, editableWhen:'#{root.allowRangeColumnEdit[item]}' ),
            new Column(name:'rate', caption:'Rate (%)*', type:'decimal', editable:true, editableWhen:'#{root.allowRangeColumnEdit[item]}' ),
        ]},
        validate     : { li -> doValidateAssessLevelRange( li.item ) },
        onAddItem    : { item -> 
            selectedAssessLevel.ranges.add( item ) 
            svc.saveAssessLevel( selectedAssessLevel )
        },
        onRemoveItem   : { item -> doRemoveRangeItem( item ) },
        fetchList    : { return selectedAssessLevel.ranges  },
    ] as SubListModel
    
    def allowRangeColumnEdit = { item ->
        if( ! selectedAssessLevel ) return false
        if( selectedAssessLevel.fixrate == true ) return false
        if( isFirstItem( selectedAssessLevel.ranges ) ) return true
        if( isNewEntry( item.mvfrom ) ) return true
        return isLastItem( selectedAssessLevel.ranges, item )
    } as Map
    
    void doValidateAssessLevel( al ) {
        assessLevelMsg = null 
        try {
            required(al.classcode, 'Code')
            required(al.classname, 'Name')
            validateRate( al.rate )
            checkDuplicateAssessLevel( al )
            svc.saveAssessLevel( al ) 
        }
        catch( e ) {
            assessLevelMsg = e.message
            binding.refresh('assessLevelMsg') 
            throw e 
        }
    }
    
    void checkDuplicateAssessLevel( al ) {
        def item = assessLevels.find{ it.objid != al.objid && (it.classcode == al.classcode || it.classname == al.classname )  }
        if( item ) throw new Exception('Duplicate item is not allowed.')    
    }
    
    void doRemoveRangeItem( item ) {
        if( isLastItem( selectedAssessLevel.ranges, item ) ) {
            selectedAssessLevel.ranges.remove( item )
            svc.saveAssessLevel( selectedAssessLevel )
        }
    }
    
    void doValidateAssessLevelRange( range ) {
        def lastRange = null 
        if( selectedAssessLevel.ranges ) {
            lastRange = selectedAssessLevel.ranges.get( selectedAssessLevel.ranges.size() - 1 )
        }
        range.mvfrom = (lastRange ? lastRange.mvto : 0.0)
        
        assessLevelMsg = null
        try {
            if( range.mvto < range.mvfrom && range.mvto != 0  ) throw new Exception('MV To must be greater than MV From')
            validateRate( range.rate )
            svc.saveAssessLevel( selectedAssessLevel )
        }
        catch( e ) {
            assessLevelMsg = e.message 
            binding.refresh('assessLevelMsg') 
            throw e 
        }
    }
    
    /*---------------------------------------------------------------------
    *
    * LCUV Support
    *
    ---------------------------------------------------------------------*/
    def selectedLCUV;
    def selectedSpecificClass;
    def selectedSubClass;
    def selectedStripping;
    def lcuvMsg;
    def lcuv = [];

    
    def lcuvListHandler = [
        createItem : { return createLCUV() },
        getRows    : { return 50 },
        getColumns : { return [
            new Column(name:'classname', caption:'Classification', editable:true ),
        ]},
        validate       : { li -> doValidateLCUV( li.item) },
        onRemoveItem   : { item -> 
            doRemoveItem( lcuv, item) 
            svc.removeLCUV( selectedLCUV )
        },
        onAddItem      : { item -> lcuv.add( item ); },
        fetchList      : { return lcuv; },
    ] as SubListModel
    
    def areatypes = ['SQM', 'HA'];
    def specificClassListHandler = [
        createItem : { return createSpecificClass(); },
        getRows    : { return 50 },
        getColumns : { return [
            new Column(name:'classcode', caption:'Code*', maxWidth:60, editable:true, editableWhen:'#{root.allowColumnEdit[item]}' ),
            new Column(name:'classname', caption:'Name*', editable:true, editableWhen:'#{root.allowColumnEdit[item]}' ),
            new Column(name:'areatype', caption:'Area Type*', type:'combo', items:areatypes , editableWhen:'#{root.allowColumnEdit[item]}' ),
        ]},
        validate     : { li -> doValidateSpecificClass( li.item ) },
        onAddItem    : { item -> 
            svc.saveLCUVSpecificClass( item );
            selectedLCUV.specificclass.add( item );
        },
        onRemoveItem   : { item -> doRemoveSpecificClassItem( item ) },
        fetchList    : { return selectedLCUV?.specificclass; },
    ] as SubListModel
    
    def subClassListHandler = [
        createItem : { return createSubClass(); },
        getColumns : { return [
            new Column(name:'subclasscode', caption:'Code*', maxWidth:60, editable:true, editableWhen:'#{root.allowSubClassColumnEdit[item]}' ),
            new Column(name:'subclassname', caption:'Name*', editable:true, editableWhen:'#{root.allowSubClassColumnEdit[item]}' ),
            new Column(name:'unitvalue', caption:'Unit Value*', type:'decimal', editable:true, editableWhen:'#{root.allowSubClassColumnEdit[item]}' ),
        ]},
        validate     : { li -> doValidateSubClass( li.item ) },
        onAddItem    : { item -> selectedSpecificClass.subclass.add( item ); },
        onRemoveItem   : { item -> doRemoveSubClassItem( item ) },
        fetchList    : { return selectedSpecificClass?.subclass; },
    ] as SubListModel
    
    def strippingListHandler = [
        createItem : { return createStripping(); },
        getRows    : { return 50 },
        getColumns : { return [
            new Column(name:'striplevel', caption:'Stripping Level*', type:'integer', editable:true, editableWhen:'#{root.allowColumnEdit[item]}' ),
            new Column(name:'rate', caption:'Rate (%)*', type:'decimal', editable:true, editableWhen:'#{root.allowColumnEdit[item]}' ),
        ]},
        validate     : { li -> doValidateStripping( li.item ) },
        onAddItem    : { item -> selectedLCUV.stripping.add( item ); },
        onRemoveItem   : { item -> doRemoveStrippingItem( item ) },
        fetchList    : { return selectedLCUV.stripping; },
    ] as SubListModel
    
    def allowColumnEdit = { item ->
        if( !selectedLCUV.classcode ) return false;
        return true;
    } as Map
    
    def allowSubClassColumnEdit = { item ->
        if( !selectedLCUV.classcode ) return false;
        if( !selectedSpecificClass.classcode ) return false;
        return true;
    } as Map
    
    void doValidateLCUV( al ) {
        lcuvMsg = null 
        try {
            required(al.classname, 'Classification')
            checkDuplicateLCUV( al );
            svc.saveLCUV( al ); 
        }
        catch( e ) {
            lcuvMsg = e.message
            binding.refresh('lcuvMsg') 
            throw e 
        }
    }
    
    void checkDuplicateLCUV( al ) {
        def item = lcuv.find{ it.objid != al.objid && ( it.classname == al.classname )  }
        if( item ) throw new Exception('Duplicate item is not allowed.')    
    }
    
    void doRemoveSpecificClassItem( item ) {
        if( MsgBox.confirm('Delete selected item?') ) {
            selectedLCUV.specificclass.remove( item );
            svc.removeLCUVSpecificClass( item );
        }
    }
    
    void doValidateSpecificClass( sc ) {
        try {
            required( sc.classcode, 'Code' );
            required( sc.classname, 'Name' );
            required( sc.areatype, 'Area Type' );
            
            checkDuplicate( selectedLCUV.specificclass, 'Code', 'classcode', sc.objid, sc.classcode )
            checkDuplicate( selectedLCUV.specificclass, 'Name', 'classname', sc.objid, sc.classname )
            svc.saveLCUVSpecificClass( sc );
            clearLcuvMsg()
        }
        catch( e ) {
            lcuvMsg = e.message 
            binding.refresh('lcuvMsg') 
            throw e 
        }
    }
    
    void doRemoveSubClassItem( item ) {
        if( MsgBox.confirm('Delete selected item?') ) {
            svc.removeLCUVSubClass( item );
            selectedSpecificClass.subclass.remove( item );
        }
    }
    
    void doValidateSubClass( sc ) {
        try {
            required( sc.subclasscode, 'Code' );
            required( sc.subclassname, 'Name' );
            if( sc.unitvalue == null ) throw new Exception('Unit Value is required.')
            checkDuplicate( selectedSpecificClass.subclass, 'Code', 'subclasscode', sc.objid, sc.subclasscode )
            checkDuplicate( selectedSpecificClass.subclass, 'Name', 'subclassname', sc.objid, sc.subclassname )
            sc.specificclassid = selectedSpecificClass.objid 
            svc.saveLCUVSubClass( sc );
            clearLcuvMsg()
        }
        catch( e ) {
            lcuvMsg = e.message 
            binding.refresh('lcuvMsg') 
            throw e 
        }
    }
    
    void checkDuplicate( list, caption, field, objid, value ) {
        def data = list.find{ it.objid != objid && it[field] == value }
        if( data ) throw new Exception('Duplicate ' + caption + ' is not allowed.')
    }
    
    void clearLcuvMsg() {
        lcuvMsg = null
        binding.refresh( 'lcuvMsg' ) 
    }
    
    boolean existsSubclass( sc ) {
        if( selectedSpecificClass ) {
            return selectedSpecificClass.subclass.findAll{ it.objid == sc.objid }.size() > 0
        }
        return false 
    }
    
    void doRemoveStrippingItem( item ) {
        selectedLCUV.stripping.remove( item );
        svc.removeLCUVStripping( item );
    }
    
    void doValidateStripping( st ) {
        try {
            required( st.striplevel, 'Strip Level' );
            checkDuplicate( selectedLCUV.stripping, 'Strip Level', 'striplevel', st.objid, st.striplevel )
            validateRate( st.rate );
            svc.saveLCUVStripping( st );
            clearLcuvMsg()
        }
        catch( e ) {
            lcuvMsg = e.message 
            binding.refresh('lcuvMsg') 
            throw e 
        }
    }
    
    void setSelectedLCUV( item ){
        selectedLCUV = item;
        
        if( selectedLCUV ){ 
            selectedLCUV.specificclass = svc.openSpecificClass( selectedLCUV.objid );
            selectedLCUV.stripping = svc.openStripping( selectedLCUV.objid );
        } else{ 
            selectedLCUV.specificclass = [];
            selectedLCUV.stripping = [];
        }
        specificClassListHandler.load()
        strippingListHandler.load()
    }
    
    void setSelectedSpecificClass( item ){
        selectedSpecificClass = item;
        
        if( selectedSpecificClass ) selectedSpecificClass.subclass = svc.openSubClass( selectedSpecificClass.objid );
        else selectedSpecificClass.subclass = [];
        subClassListHandler.load()
    }
    
    
    /*---------------------------------------------------------------------
    *
    * LandAdjustment Support
    *
    ---------------------------------------------------------------------*/
    def selectedLandAdjustment;
    
    
    def landAdjustmentListHandler = [
        getRows    : { return 50 },
        getColumns : { return [
            new Column(name:'adjustmentcode', caption:'Code', maxWidth:60),
            new Column(name:'adjustmentname', caption:'Name'),
            new Column(name:'appliedto', caption:'Applied To', maxWidth:150 ),
            new Column(name:'expression', caption:'Expression'),
        ]},
        onRemoveItem   : { item -> doRemoveLandAdjustmentItem( item ) },
        fetchList      : { return landadjustments; },
    ] as SubListModel 

    
    def addLandAdjustmentHandler = { item -> 
        item.appliedto = item.classifications.propertycode.join(',');
        item.classids = item.classifications.propertyid.join('/');
        item.landrysettingid = entity.objid
        svc.saveLandAdjustment( item ) 
        landadjustments.add( item )
        landAdjustmentListHandler.load()
    }
    
    def updateLandAdjustmentHandler = { item -> 
        item.appliedto = item.classifications.propertycode.join(',');
        item.classids = item.classifications.propertyid.join('/');
        svc.saveLandAdjustment( item ) 
        landadjustments.set( landadjustments.indexOf( selectedLandAdjustment ), item )
        binding.refresh('selectedLandAdjustment')
    }
    
    def createLandAdjustment() {
        return InvokerUtil.lookupOpener('landadjustment.create', [addHandler:addLandAdjustmentHandler, adjustments:landadjustments] )
    }
    
    def openLandAdjustment() {
        if( selectedLandAdjustment ) {
            return InvokerUtil.lookupOpener('landadjustment.open', [entity:selectedLandAdjustment, updateHandler:updateLandAdjustmentHandler, adjustments:landadjustments] )
        }
    }
    
    void removeLandAdjustment() {
        if( selectedLandAdjustment ) {
            doRemoveLandAdjustmentItem( selectedLandAdjustment )
        }
    }
    
    
    void doRemoveLandAdjustmentItem( item ) {
        if( MsgBox.confirm('Remove item?') ) {
            svc.removeLandAdjustment( selectedLandAdjustment )
            landadjustments.remove( item )
        }
    }
    
    
    /*---------------------------------------------------------------------
    *
    * LandActualUseAdjustment Support
    *
    ---------------------------------------------------------------------*/
    def selectedLandActualUseAdjustment;
    
    
    def landActualUseAdjustmentListHandler = [
        getRows    : { return 50 },
        getColumns : { return [
            new Column(name:'adjustmentcode', caption:'Code', maxWidth:60),
            new Column(name:'adjustmentname', caption:'Name'),
            new Column(name:'appliedto', caption:'Applied To' ),
            new Column(name:'expression', caption:'Expression'),
        ]},
        onRemoveItem   : { item -> doRemoveLandActualUseAdjustment( item ) },
        fetchList      : { return landactualuseadjustments; },
    ] as SubListModel 

    
    def addLandActualUseAdjustmentHandler = { item -> 
        item.appliedto = item.classifications.propertycode.join(',');
        item.classids = item.classifications.propertyid.join('/');
        item.landrysettingid = entity.objid
        svc.saveLandActualUseAdjustment( item ) 
        landactualuseadjustments.add( item )
        landActualUseAdjustmentListHandler.load()
    }
    
    def updateLandActualUseAdjustmentHandler = { item -> 
        item.appliedto = item.classifications.propertycode.join(',');
        item.classids = item.classifications.propertyid.join('/');
        svc.saveLandActualUseAdjustment( item ) 
        binding.refresh('selectedLandActualUseAdjustment')
    }
    
    def createLandActualUseAdjustment() {
        return InvokerUtil.lookupOpener('landactualuseadjustment.create', [addHandler:addLandActualUseAdjustmentHandler, adjustments:landactualuseadjustments] )
    }
    
    def openLandActualUseAdjustment() {
        return InvokerUtil.lookupOpener('landadjustment.open', [entity:selectedLandActualUseAdjustment, updateHandler:updateLandActualUseAdjustmentHandler, adjustments:landactualuseadjustments] )
    }
    
    void removeLandActualUseAdjustment() {
        if( selectedLandActualUseAdjustment ) {
            doRemoveLandActualUseAdjustment( selectedLandActualUseAdjustment )
        }
    }
    
    void doRemoveLandActualUseAdjustment( item ) {
        if( MsgBox.confirm('Remove item?') ) {
            svc.removeActualUseLandAdjustment( selectedLandActualUseAdjustment  )
            landactualuseadjustments.remove( item )
        }
    }
    
    
    
    /*---------------------------------------------------------------------
    * Create Records Support
    ---------------------------------------------------------------------*/
    Map createEntity() {
        return [
            objid       : 'LRY' + new UID(),
            ry          : null
        ]
    }
    
    Map createAssessLevel() {
        return [ 
            objid           : 'AL' + new UID(),
            landrysettingid : entity.objid,
            fixrate         : true,
            rate            : 0.0,
            ranges          : [],
        ]
    }
    
    Map createRangeAssessLevel() {
        return [ 
            mvfrom  : null,
            mvto    : null,
            rate    : 0.0,
        ]
    }
    
    Map createLCUV() {
        return [ 
            objid           : 'AL' + new UID(),
            landrysettingid : entity.objid,
        ]
    }
    
    Map createSpecificClass() {
        return [ 
            objid           : 'SC' + new UID(),
            areatype        : 'SQM',
            lcuvid          : selectedLCUV.objid,
            landrysettingid : entity.objid
        ]
    }
    
    Map createSubClass() {
        return [ 
            objid           : 'SC' + new UID(),
            unitvalue       : 0.0,
            landrysettingid : entity.objid,
        ]
    }
    
    Map createStripping() {
        return [ 
            objid           : 'ST' + new UID(),
            rate            : 0.0,
            lcuvid          : selectedLCUV.objid,
            landrysettingid : entity.objid,
        ]
    }
    
    /*---------------------------------------------------------------------
    * Common Methods 
    ---------------------------------------------------------------------*/
    void required( value, caption ) {
        if( ! value ) throw new Exception(caption + ' is required.') 
    }
    
    void doRemoveItem( list, item ) {
        if( MsgBox.confirm('Remove item?') ) {
            list.remove( item )
        }
    }
    
    void validateRate( rate ) {
        if( rate < 0.0 ) throw new Exception('Rate must be greater than or equal to zero.')
        if( rate > 100) throw new Exception('Rate must not exceed 100.00')
    }
    
    def isFirstItem( list ) {
        return list.size() == 0
    }
    
    def isNewEntry( value ) {
        return value == null 
    }
    
    def isLastItem( list, item ) {
        return list.size() == list.indexOf( item ) + 1
    }
}


]]>

    </code>
    
    <pages>
        <page template="etracs2.rpt.ry.land.LandRYSettingPage"/>
        <page name="new" template="etracs2.rpt.ry.RYSettingNewPage"/>
        <page name="copy" template="etracs2.rpt.ry.RYSettingCopyPage"/>
    </pages>
</workunit>
