import com.rameses.annotations.*;
import com.rameses.util.*;

class TerminalService {

    @PersistenceContext("main")
    def em; 		

    @Service("DateService")
    def dateSvc;	

    //allocates terminal sizes	
    @ProxyMethod	
    public void allocate( x ) {
		assert x.size > 0; 
		
		int sz = x.remove("size");
		for( int i=0; i< sz; i++) {
			x.terminalid = KeyGen.generateAlphanumKey( null, 10 );
			x.state = 0;
			em.create( "terminal", x );		
		}
    }

	@ProxyMethod
    public def getList( param ) {
        def qry = em.sqlContext.createNamedQuery("terminal:list.sql").setParameters(param);
		if( param._start ) qry.setFirstResult(param._start);
		if( param._limit ) qry.setMaxResults(param._limit);
		return qry.resultList;
    }

    @ProxyMethod	
	public def register( def o ) {
		//search first if terminal exists
		def terminal = em.read("terminal", o );
		if(!terminal) throw new Exception("Terminal id does not exist");
		
		if(terminal.macaddress) {
			if( terminal.macaddress == o.macaddress) 
				throw new Exception("This Terminal ID already exists. Please choose the recover option instead"); 
			else 	
				throw new Exception("This Terminal ID is already assigned"); 
		}
		
		def test = em.sqlContext.createNamedQuery("terminal:find-macaddress").setParameter("macaddress",o.macaddress).singleResult;
		if(test) throw new Exception("This machine is already assigned to " + test.parentcode + ". Use terminal recovery instead" );
		terminal.macaddress = o.macaddress;
		terminal.registeredby = o.registeredby;
		terminal.dtregistered = dateSvc.serverDate;
		return em.update("terminal", terminal );
	}

    @ProxyMethod	
	public def recover( def o ) {
		assert o.macaddress;
		def terminal = em.sqlContext.createNamedQuery("terminal:find-macaddress").setParameter("macaddress",o.macaddress).singleResult;
		if(!terminal) throw new Exception("Terminal is not registered");
		return terminal;	
	}
	
	/**
	* checks first client terminal id
	* if verified, returns the parentid 
 	*/
	@ProxyMethod
	public def verifyClient( String terminalid, String clientcode ) {
		if( !terminalid ) throw new Exception("Terminal ID must be provided");
		def terminal = em.read("terminal", [terminalid: terminalid] );
		if(!terminal)
			throw new Exception("Terminal does not exist");
		if(terminal.parentcode != clientcode)
			throw new Exception("Terminal is not registered with " + clientcode );
		return terminal;		
	}
	
	@ProxyMethod	
	public def unregister( def o ) {
		em.sqlContext.createNamedExecutor("terminal:unregister-terminal").setParameters(o).execute();
	}
	
	@ProxyMethod	
    public def getGroupTerminals( def param ) {
		def list = em.sqlContext.createNamedQuery( "terminal:list").setParameters( param ).resultList;
		if(param.excludes) {
			return list.findAll{ !param.excludes.contains( it.terminalid )  }
		}	
		else	
			return list;
	}
	

}